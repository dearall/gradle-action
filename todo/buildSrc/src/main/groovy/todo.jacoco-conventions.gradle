plugins {
    id 'todo.java-conventions'
    id 'jacoco'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    reports {
        xml.required = false
        csv.required = false
        html.required = true
//        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

// Define dependencies between code coverage reports and test execution
test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

tasks.register('integrationTestReport', JacocoReport) {
    executionData integrationTest
    sourceSets sourceSets.integrationTest

    dependsOn integrationTest

    reports {
        xml.required = false
        csv.required = false
        html.required = true
    }
}

integrationTest {
    finalizedBy integrationTestReport // report is always generated after tests run
}

tasks.named('check'){
    dependsOn jacocoTestReport, integrationTestReport
}