plugins {
    id 'todo.java-library'
    id 'todo.maven-publish-jar-conventions'
}

apply from: 'databaseSetup.gradle'

dependencies {
    implementation project(':todo-model')

    testImplementation(libs.hamcrest.core)
    testImplementation(libs.slf4j.simple)
    runtimeOnly(libs.h2)
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives groovydocJar
}

tasks.register('testNG', Test) {
    useTestNG()
    include "**/*NGTest"
}

test {
    // 从默认 test 中排除 testNG 测试
    exclude "**/*NGTest"
    dependsOn 'testNG'
}

tasks.named('integrationTest'){
    dependsOn startAndPrepareDatabase
    finalizedBy stopDatabase
}

publishing.publications.named(ext.publishName) {
    pom {
        name = "todo-repository"
        description = 'Gradle plugin for managing applications and databases on CloudBees RUN@cloud.'
        url = "https://github.com/dearall/todo.git"
        inceptionYear = '2021'
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = 'repo'
            }
        }
        developers {
            developer {
                id = "johnd"
                name = "John Doe"
                email = "john.doe@example.com"
            }
        }
        scm {
            connection = "scm:git:https://github.com/dearall/todo.git"
            developerConnection = "scm:git:https://github.com/dearall/todo.git"
            url = "https://github.com/dearall/todo.git"
        }
    }
}
